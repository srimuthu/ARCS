sort 
	TSt = struct ARR?isArr|DEP?isDep|NT?isNT|TRAIN?isTrain|BROKEN?isBroken;
	ASt = struct AON?isAOn|AOFF?isAOff;
	BSt = struct OPEN?isOpen|CLOSE?isClosed;
	DTime = struct t0?ist0|ts?ists|tf?istf;
	WSt = struct WLOW?isWLow|WHIGH?isWHigh|WOFF?isWOff;
	SSt = struct GREEN?isGreen|YELLOW?isYellow|RED?isRed;
	TSp = struct slow|fast;
map 
	TSp2DTime:TSp->DTime;
	TSt2DTime:TSt->DTime;
	
eqn
	TSp2DTime(slow)=ts;
	TSp2DTime(fast)=tf;
	TSt2DTime(NT)=t0;
	TSt2DTime(BROKEN)=t0;
act
	%Train States
	%Tr_state,Tr_state_,
	Tr_state__: TSt;
	Tx_time,Send_time,Recv_time,Set_time_,Set_time__,Exp_time : DTime;
	Al_set: ASt;
	Sig_set : SSt;
	Bar_set : BSt;
	Warn_set : WSt;
	BarC_ack_,Tr_imm,Inter;
	BarO_,BarO__,BarO;
	Sig_ack,Sig_ack_,Sig_ack__;
	BarO_ack_,BarO_ack__,Clear_sig;
	RESET_,RESET__,RESET;
	Fix_sensor;
%	Override:Bool;
proc
		
	ARCS_TRAIN(Tstate:TSt) = 
	(isNT(Tstate))
	->(
	Tr_state__(ARR).(ARCS_TRAIN(BROKEN)+sum spd:TSp.(Send_time(TSp2DTime(spd)))).Tr_imm.ARCS_TRAIN(TRAIN)
	+
	Tr_state__(BROKEN).Send_time(TSt2DTime(BROKEN)).Tr_imm.Fix_sensor.RESET_.Sig_ack__.ARCS_TRAIN(NT)
	)
	+
	(isDep(Tstate))
	->Tr_state__(DEP).BarO_.Sig_ack__.(ARCS_TRAIN(NT))
	+
	(isTrain(Tstate))
	->Tr_state__(TRAIN).(ARCS_TRAIN(BROKEN)+ARCS_TRAIN(DEP))
	+
	(isBroken(Tstate))
	->Tr_state__(BROKEN).Send_time(TSt2DTime(BROKEN)).Tr_imm.Fix_sensor.RESET_.Sig_ack__.ARCS_TRAIN(NT)
	;


	ARCS_BARRIER(state:BSt) = 
	%%Receive a Timer
	(isOpen(state))
	->(
		Set_time__(t0).Bar_set(CLOSE).BarC_ack_.ARCS_BARRIER(CLOSE)
	)
	
	+
	(isClosed(state))
	->(
		(BarO__.Bar_set(OPEN).BarO_ack_.ARCS_BARRIER(OPEN))
		+
		(RESET__.Bar_set(OPEN).BarO_ack_.ARCS_BARRIER(OPEN))
		+
		Set_time__(t0).BarC_ack_.ARCS_BARRIER(CLOSE)
	);
		
	ARCS_SIGNALS(sig:SSt,warn:WSt,alarm:ASt) =
	(Recv_time(t0).Sig_set(RED).Warn_set(WHIGH).Al_set(AON).Set_time_(t0).ARCS_SIGNALS(RED,WHIGH,AON))
	+
	(isGreen(sig)&&isWOff(warn)&&isAOff(alarm))
	->(	
		(Recv_time(ts).Sig_set(YELLOW).Warn_set(WLOW).Al_set(AOFF).ARCS_SIGNALS(YELLOW,WLOW,AOFF))
		+
		(Recv_time(tf).Sig_set(YELLOW).Warn_set(WLOW).Al_set(AOFF).ARCS_SIGNALS(YELLOW,WLOW,AOFF))
	)
	+
	(isYellow(sig)&&isWLow(warn)&&isAOff(alarm))
	->(
		(Sig_set(RED).Warn_set(WHIGH).Al_set(AON).Set_time_(t0).ARCS_SIGNALS(RED,WHIGH,AON))
	)
	+
	(isRed(sig)&&isWHigh(warn)&&isAOn(alarm))
	->(
		(BarO_ack__.Sig_set(GREEN).Warn_set(WOFF).Al_set(AOFF).Sig_ack_.ARCS_SIGNALS(GREEN,WOFF,AOFF))
%		+
%		(Reset_sig__.Sig_set(GREEN).Warn_set(WOFF).Al_set(AOFF).Sig_ack_.ARCS_SIGNALS(GREEN,WOFF,AOFF))
	);
	
init
hide(
	{Clear_sig,	BarO,Sig_ack,Inter},
	allow({ Al_set,Bar_set,Sig_set,Warn_set,Tr_state__,Clear_sig,BarO,RESET,
			Tx_time,Exp_time,Inter,Sig_ack,Fix_sensor},
		comm({
				%Tr_state_|Tr_state__->Tr_state,
				Send_time|Recv_time->Tx_time,
				Set_time_|Set_time__->Exp_time,
				BarC_ack_|Tr_imm->Inter,
				BarO_ack_|BarO_ack__->Clear_sig,
				Sig_ack_|Sig_ack__->Sig_ack,
				BarO_|BarO__->BarO,
				RESET_|RESET__->RESET
				},
				ARCS_TRAIN(NT)||ARCS_BARRIER(OPEN)||ARCS_SIGNALS(GREEN,WOFF,AOFF)
		)
		));